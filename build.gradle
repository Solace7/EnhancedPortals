
buildscript {
    repositories {
        mavenCentral()

        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }

        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/uo"
        }

    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = config.version
group = config.mod_id
archivesBaseName = "EnhancedPortals_${config.minecraft_version}"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    runDir = "idea"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand ([
            'mod_version': version,
            'mc_version': project.config.minecraft_version,
            'mod_id': project.config.mod_id
        ])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

repositories {
    maven {
        name = "Sangar OC"
        url = "http://maven.cil.li/"
    }

    maven {
        name = "TechNut GuideAPI"
        url = 'http://tehnut.info/maven/'
    }
}

dependencies {
    compile "li.cil.oc:OpenComputers:MC" + project.config.minecraft_version + "-" + project.config.oc_version + ":api"
    compile "info.amerifrance.guideapi:Guide-API:" + project.config.minecraft_version + "-" + project.config.guideapi_version + ":deobf"
}

jar {
    appendix = 'universal'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
}

artifacts {
    archives deobfJar
}

idea {
    module {
        // For some reason this is necessary for IDEA 2014 workspaces to recognize assets
        inheritOutputDirs = true
    }
}
