buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }

    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = targetCompatibility = "1.6" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.6"
}

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = config.version
group = config.mod_id
archivesBaseName = "EnhancedPortals_${project.config.mc_version}"

minecraft {
    version = "1.10.2-12.18.3.2185"
    runDir = "idea"
    mappings = project.config.mappings_version
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand ([
            'mod_version': version,
            'mc_version': project.config.mc_version,
            'mod_id': project.config.mod_id
        ])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

repositories {

    maven {
        url "http://maven.epoxide.xyz"
    }

    maven {
        name = "Sangar OC"
        url = "http://maven.cil.li/"
    }

    maven {
        name = "TechNut GuideAPI"
        url = 'http://tehnut.info/maven/'
    }

    maven {
        // location of the maven that hosts JEI files
        url = "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
// add -Dfml.coreMods.load=li.cil.oc.common.launch.TransformerLoader to VM options
    compile "li.cil.oc:OpenComputers:MC${project.config.mc_version}-${project.config.oc_version}"
    deobfCompile "info.amerifrance.guideapi:Guide-API:${project.config.mc_version}-${project.config.guideapi_version}"
    deobfCompile "mezz.jei:jei_${project.config.mc_version}:${project.config.jei_version}"
    compile "net.darkhax.tesla:Tesla:${project.config.mc_version}-${project.config.tesla_version}"
}

jar {
    appendix = 'universal'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
}

artifacts {
    archives deobfJar
}

idea {
    module {
        // For some reason this is necessary for IDEA 2014 workspaces to recognize assets
        inheritOutputDirs = true
    }
}
